<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MAIN" Id="{3a75e61e-fd60-434a-bbda-881b04f7f156}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	// Invoker
	fbSwitchPanel				: FB_SwitchPanel;
	
	// Receiver
	fbSocket					: FB_Socket;
	refSocket					: REFERENCE TO FB_Socket := fbSocket;	
	fbLamp						: FB_Lamp;
	refLamp						: REFERENCE TO FB_Lamp := fbLamp;	
	fbAirConditioning			: FB_AirConditioning;
	refAirConditioning			: REFERENCE TO FB_AirConditioning := fbAirConditioning;
	fbCDPlayer					: FB_CDPlayer;
	refCDPlayer					: REFERENCE TO FB_CDPlayer := fbCDPlayer;

	// Commands
	fbSocketOnCommand			: FB_SocketOnCommand(refSocket);
	fbSocketOffCommand			: FB_SocketOffCommand(refSocket);
	fbLampSetLevel100Command	: FB_LampSetLevelCommand(refLamp, 100);
	fbLampSetLevel0Command		: FB_LampSetLevelCommand(refLamp, 0);
	fbAirConComfortCommand		: FB_AirConComfortCommand(refAirConditioning);
	fbAirConStandbyCommand		: FB_AirConStandbyCommand(refAirConditioning);	
	fbMusicPlayCommand			: FB_MusicPlayCommand(refCDPlayer);
	fbMusicStopCommand			: FB_MusicStopCommand(refCDPlayer);	
	fbRoomOffCommand			: FB_RoomOffCommand(fbSocketOffCommand, fbLampSetLevel0Command, fbAirConStandbyCommand, fbMusicStopCommand);	
	bInit						: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF (NOT bInit) THEN
	fbSwitchPanel.SetCommand(1, fbSocketOnCommand);
	fbSwitchPanel.SetCommand(2, fbSocketOffCommand);
	fbSwitchPanel.SetCommand(3, fbLampSetLevel100Command);
	fbSwitchPanel.SetCommand(4, fbLampSetLevel0Command);
	fbSwitchPanel.SetCommand(5, fbAirConComfortCommand);
	fbSwitchPanel.SetCommand(6, fbAirConStandbyCommand);
	fbSwitchPanel.SetCommand(7, fbMusicPlayCommand);
	fbSwitchPanel.SetCommand(8, fbRoomOffCommand);
	bInit := TRUE;
ELSE
	fbSwitchPanel();	
END_IF


]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>