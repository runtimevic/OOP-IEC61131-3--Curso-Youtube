### <span style="color:grey">Puntero y Referencia:</span>
En la programaci√≥n orientada a objetos (OOP) en PLC IEC 61131-3, los punteros y las referencias son dos conceptos importantes que se utilizan para acceder a los datos y m√©todos de un objeto. 
Un puntero es una variable que almacena la direcci√≥n de memoria de otra variable. 
Una referencia es una variable que se utiliza para acceder a otra variable sin tener que conocer su direcci√≥n de memoria.
### <span style="color:grey">. ¬øQu√© es un puntero?</span>

- Es un dato que apunta o se√±ala hacia una direcci√≥n de memoria.
- Es una variable que contiene la direcci√≥n de memoria donde ‚Äúvive‚Äù la variable.
- Con el empleo de punteros se accede a la memoria de forma directa,por lo que es una buena t√©cnica para reducir el tiempo de ejecuci√≥n de un programa y otras muchas m√°s funcionalidades.
### <span style="color:grey">. Tipos de Punteros:</span>

- Hay un tipo de puntero para cada tipo de dato, programa, Function Block, funciones, etc. 
- Seg√∫n sea el ‚Äúobjeto‚Äù al que se desea acceder se necesita un puntero de un tipo u otro.

### <span style="color:grey">. Declaraci√≥n de punteros:</span>

El compilador necesita conocer todos los punteros que se vayan a emplear en el proyecto,
por lo que hay que declararlos, como cualquier otra variable. 
En el c√≥digo se muestra el script necesario para la declaraci√≥n de varios tipos de punteros:

```javascript
// Un puntero no deja de ser una variable, la diferencia est√° en que su contenido no es un valor determinado sino que es la direcci√≥n 
// de memoria donde se ubica la variable de la que se quiere leer o escribir su valor. Y al igual que hay que declarar todas las
// variables del tipo correspondiente. tambi√©n hay que declarar todas las variables -punteros- que contendr√°n esas direcciones de 
// memoria y su correspondiente tipo.
VAR
    stTest1 : stTipo1; //Declara una estructura de datos del tipo stTipo1.

    pin01 : POINTER TO INT; //Declara un puntero para acceder a variables del tipo INT.
    ps20  : POINTER TO STRING[20]; //Declara un puntero para acceder a variables del tipo STRING de 20 caracteres.
    pa20 : POINTER TO ARRAY [1..20] OF INT; //Declara un puntero para acceder a variables del tipo ARRAY de 20 elementos del tipo INT.
    pDword : POINTER TO DWORD; //Declara un puntero para acceder a variables del tipo DWORD.
    past1  : POINTER TO stTipo1; //Declara un puntero para acceder a variables del tipo stTipo1.
    pReal  : POINTER TO REAL; //Declara un puntero para acceder a variables del tipo REAL.
END_VAR
```
### <span style="color:grey">. Como saber qu√© direcci√≥n asignar al puntero:</span>

- Para poder acceder a una variable mediante un puntero se necesita conocer su direcci√≥n de memoria, 
Para ello se dispone de un operador llamado **ADR** que asigna la direcci√≥n de la variable deseada, al puntero.
- Es conveniente verificar que el valor del puntero no es cero, antes de utilizarlo.
Por otra parte, para poder leer / escribir el valor de la variable, a la que se√±ala el puntero, 
se dispone del operador de contenido **^**. Cuando se hace referencia al contenido, de la direcci√≥n de memoria apuntada, se habla de desreferenciar el puntero. En el siguiente c√≥digo se muestra un ejemplo:

```javascript
PROGRAM SR_Main_02
VAR
    in01    : INT; //Declaraci√≥n de la variable in01 de tipo entero.
    in02    : INT := 123; //Declaraci√≥n e inicializaci√≥n de la variable in02 de tipo entero.
    in03    : INT; //Declaraci√≥n de la variable in03 de tipo entero.

    pint    : POINTER TO INT; //Declaraci√≥n de un puntero para acceder a variables del tipo entero.
END_VAR

// Ejemplo de uso b√°sico de los operadores ADR y del operador de contenido ^
// Se muestra como asignar a un puntero la direcci√≥n de memoria de una variable y como leer/escribir
// as√≠ como un ejemplo de acceso a variables locales de otros programas.

pint := ADR(in01); //Asignamos al puntero la direcci√≥n de memoria donde se ubica la variable in01.
pint^ := 44;     //A la posici√≥n de memoria indicada por el puntero, le asignamos el valor 44
                 //Por tanto a la variable in01 se le ha escrito el valor 44.

in02 := in01; // in02 ser√° igual a 44.

pint := ADR(in02); //Cambiamos la direcci√≥n para acceder a la direcci√≥n de la variable in02.
in03 := pint^; // in03 tomara el valor del contenido de la posici√≥n de memoria contenida en el
               // que hemos asignado la direcci√≥n de in02, por tanto in03= 123.

pint := ADR(SR_Main_01.inLocalAway); //Cargamos la direcci√≥n de memoria de una variable local de 
                                     // otro programa, la que ser√≠a inaccesible por otros medios.
pint^ := 240 ; // La varible local del programa SR_Main_01.inLocalAway tomar√° el valor 240                                                
```

### <span style="color:grey">. ¬øQu√© es un acceso indirecto?</span>

Lo primero, decir que no tiene nada que ver con un puntero.
Un acceso indirecto permite elegir un n√∫mero de elemento dentro de un array, hay una variable, llamada √≠ndice, que contiene el n√∫mero del elemento del array al que se desea acceder. En este caso no se puede acceder a ninguna otra variable m√°s all√° de los elementos del array, insisto en que no tiene nada que ver con los punteros.
Con un puntero se puede acceder a cualquier dato u objeto que est√© en la memoria del control. Con un acceso indirecto solo se puede acceder a los elementos de un array.
En el siguiente c√≥digo se muestra unos ejemplos de acceso indirecto a un array:

```javascript
PROGRAM SR_Main_01
VAR
    aR20: ARRAY[1..20] OF REAL; //Declara un array de 20 elementos del tipo REAL.
    inIndex: INT; //Declara la variable de indice del array para el acceso indirecto
    xNewVal: BOOL; //Indica que hay una nueva lectura del sensor de fuerza.
    rFuerza: REAL; //Valor de fuerza del sensor.
END_VAR

// Ejemplo01: Se asigna valores del 1 al 20 a cada elemento del array mediante un bucle.
FOR inIndex:=1 TO 20 BY 1 DO  // Se empieza por el valor de la variable indice a 1, hasta 20
    aR20[inIndex] := inIndex; // Al elemento aR20[inIndex] se le asigna el valor de inIndex
END_FOR;                      // Se incrementa inIndex y se repite el proceso.

// Ejemplo02: Creamos un FIFO en el que guardamos un valor anal√≥gico de fuera a cada impulso de la se√±al xNewVal.
IF xNewVal THEN                // Si hay un nuevo valor de fuerza realizamos el codigo.
    xNewVal := FALSE;          // Reset de la se√±al xNewVal.

    FOR inIndex:=20 TO 2 BY -1 DO // Variable indice a 20, hasta 2.
        aR20[inIndex] := aR20[inIndex-1]; //Desplazamiento de los valores en el FIFO --->
    END_FOR;                             // Se decrementa inIndex y se repite el proceso.

    aR20[1] := rFuerza; // Entrada del valor de fuerza en el primer elemento del FIFO.
END_IF
```
A este mismo array se puede acceder empleando un puntero, como se ver√° m√°s adelante, lo que resulta m√°s r√°pido en tiempo de ejecuci√≥n, 
pero no tan claro para quien no suele usar los punteros. 

### <span style="color:grey">. Acceso a una estructura de datos mediante punteros:</span>

El proceso es el mismo que ya se ha visto para acceder a una variable del tipo INT, pero se tendr√° que declarar un puntero del tipo adecuado, que coincida con el tipo de estructura a la que se desea acceder, ve√°moslo en el siguiente c√≥digo:

```javascript
PROGRAM SR_Main_03
VAR
    stMotor_01 : stMotorCtrl; // Estructura de control del motor 1
    stMotor_02 : stMotorCtrl; // Estructura de control del motor 2
    stMotor_03 : stMotorCtrl; // Estructura de control del motor 3
    pstMotorCtrl : POINTER TO stMotorCtrl; // Puntero para acceder a estructuras del tipo stMotorCtrl.
    xMarcha : BOOL; // Pulsador marcha motores.
END_VAR

// Ejemplo b√°sico de como acceder a estructuras de datos mediante punteros.
// La estructura de datos empleada es una llamada a stMotorCtrl, que coincide un bit de marcha, otro de paro y
// valores de velocidad en Rpms y tiempo de aceleraci√≥n/deceleraci√≥n.

// Asignamos valores a la estructura para el control del motor 1.

stMotor_01.rTpoAcelDecel := 5.4; // Tiempo para acelerar/decelerar hasta alcanazar la velocidad.
stMotor_01.rVelRpm := 1436.2; // Velocidad en RPM.
stMotor_01.xMotorOff := TRUE; // Bit de paro ON.
stMotor_01.xMotorOn := FALSE   ; // Bit de marcha OFF.

pstMotorCtrl := ADR(stMotor_01); // Cargamos la direcci√≥n de memoria de la estructura del motor 1
stMotor_02 := pstMotorCtrl^; // Copia el contenido de la zona de memoria apuntada a la
                             // estructura del motor 2, en este caso el resultado es el mismo
                             // que se obtendr√≠a con stMotor_02:= stMotor_01;

stMotor_03 := stMotor_02; // Copia los mismos valores al motor 3;

IF xMarcha THEN         // Si se pulsa marcha m√°quina
    pstMotorCtrl^.xMotorOn := TRUE; // Se activa el bit de marcha al que apunta el puntero (stMotor_01).
    pstMotorCtrl^.xMotorOff := FALSE; // Se desactiva el bit de paro al que apunta el puntero (stMotor_01)
END_IF
```

### <span style="color:grey">. Acceso a un array mediante punteros:</span>
El proceso es el mismo que ya se ha visto para acceder a una variable del tipo INT, pero se tendr√° que declarar un puntero a un array del n√∫mero de elementos y tipo de datos adecuados, 
ve√°moslo en el siguiente c√≥digo:

```javascript
PROGRAM SR_Main_03
VAR
    aintFIFO    : ARRAY[1..20] OF INT; // Array de 20 enteros.
    aintFIFO2   : ARRAY[1..20] OF INT; // Array de 20 enteros.
    paint       : POINTER TO ARRAY[1..20] OF INT; // Puntero al array.
    pint        : POINTER TO INT; // Puntero a un entero.
END_VAR

// Ejemplo basico de como acceder a arrays mediante punteros:
paint := ADR(aintFIFO); // _Asignamos la direcci√≥n del array al puntero.
paint^[3] := 4; // Dentro del array podemos acceder a un elemento en concreto
aintFIFO2 := paint^; // O copiar el array apuntado entero, sobre otro array
pint := paint + (4 * SIZEOF (INT)); // Tambien se puede crear un puntero a un INT para acceder a uno de los
                                    // elementos del array. Tomamos la direcci√≥n inicial del array y le
                                    // sumampos un offeset de tantos bytes como se necesitan para el tipo de 
                                    // datos INT y lo multiplicamos por el indice del array al que queremos
                                    // acceder. SIZEOF (TYPE) retorna el n√∫mero de bytes seg√∫n el tipo de datos.

pint^ := 5;                         // Asignamos el valor de 5, aintFIFO[5]:=5 ser√≠a lo mismo. 
```
### <span style="color:grey">. Acceso a datos por referencias:</span>
El acceso por referencia no deja de ser un acceso por puntero, pero en este caso la direcci√≥n de una referencia es la misma que la del objeto al que apunta. Un puntero tiene su propia direcci√≥n y esta contiene la direcci√≥n del objeto al que se quiere hacer referencia. 
Las referencias se inicializan al principio del programa y no pueden cambiar durante su ejecuci√≥n. 
A un puntero se le puede cambiar su direcci√≥n tanto como sea necesario durante la ejecuci√≥n del programa.
Otra forma de entender las referencias es como si fuesen otra manera de referirse a un mismo objeto/variable, como si fuese un alias.
Frente a los punteros, las referencias presentan las siguientes ventajas:

- 1) Facilidad de uso.
- 2) Sintaxis m√°s sencilla a la hora de pasar par√°metros a funciones.
- 3) Minimiza errores en la escritura del c√≥digo.

El resumen de todo esto, que se puede prestar a mucha confusi√≥n, es que, como se ver√° m√°s adelante, el gran valor de las referencias es a la hora de pasar grandes 
cantidades de datos como par√°metros de entrada a funciones.

### <span style="color:grey">. Diversas formas de pase de par√°metros a funciones:</span>

Normalmente una funci√≥n realiza unas operaciones con unos par√°metros de entrada y retorna un valor - o varios - como resultado. 
En el ejemplo que veremos seguidamente se trata de una funci√≥n para calcular el √°rea de un rect√°ngulo, a la que le pasaremos los valores del lado A y el lado B para que nos retorne el resultado del √°rea. 

Lo primero definiremos un tipo de dato [stRect√°ngulo] que contendr√° el lado A, el B y el √°rea. 

Crearemos tres rect√°ngulos, [stRectangulo01], [stRectangulo02] y [stRectangulo03]. 

Junto con tres variantes de la funci√≥n para el c√°lculo del √°rea:

- [Fc_AreaCalcVal] - pase por valores - 
- [Fc_AreaCalcPoint] - pase por puntero - 
- [Fc_AreaCalcRef] - pase por referencia ‚Äì

A continuaci√≥n, el c√≥digo de las tres funciones:

### Pase de valores:
```javascript
// Funci√≥n para calcular el area de un Rectangulo, pasando los valores de los lados del Rectangulo
// la funci√≥n retorna el resultado del area calculado

FUNCTION Fc_AreaCalcVal : REAL // La funci√≥n retona un n√∫mero real
VAR_INPUT
    i_rASide    : REAL; // Par√°metro de entrada que contiene el lado A del rectangulo.
    i_rBSide    : REAL; // Par√°metro de entrada que contiene el lado B del rectangulo.
END_VAR

Fc_AreaCalcVal := i_rASide * i_rBSide; // Retorna el resultado de multiplicar el lado A por el lado B.
```
### Pase por puntero:
```javascript
// Funci√≥n para calcular el area de un Rectangulo, con los valores contenidos en una estructura de datos del tipo stRectangulo
// La estructura se pasa mediante un puntero a la estructura stRectangulo deseada y la funci√≥n retorna el resultado a la 
// misma estructura.

FUNCTION Fc_AreaCalcPoint : REAL
VAR_INPUT
    i_ptstRect : POINTER TO st_Rectangulo; // Puntero de entrada con la direcci√≥n de la estructura.
END_VAR

// El valor del area, de la estructura indicada por la direcci√≥n del puntero es igual al
// valor del lado A de la estructura indicada por la direcci√≥n del puntero por
// el valor del lado B de la estructura indicada por la direcci√≥n del puntero
i_ptstRect^.rArea := i_ptstRect^.rASide * i_ptstRect^.rBSide;
```
### Pase por Referencia:
```javascript
// Funci√≥n para calcular el area de un Rectangulo, con los valores contenidos en una estructura de datos del tipo stRectangulo
// La estructura se pasa por referencia.

FUNCTION Fc_AreaCalcRef : REAL
VAR_INPUT
    i_Ref : REFERENCE TO st_Rectangulo;
END_VAR

i_Ref.rArea := i_Ref.rASide * i_Ref.rBSide;
```
### Ejemplo de c√≥digo de llamadas a las funciones:
```javascript
PROGRAM SR_Main_01
VAR
    inLocalAway : INT; // Variable integer local de SR_Main_01 para ser accedida externamente
    stRectangulo1 : st_Rectangulo; // Estructura que contiene los datos del rectangulo1 A, B y su area
    stRectangulo2 : st_Rectangulo; // Estructura que contiene los datos del rectangulo2 A, B y su area
    stRectangulo3 : st_Rectangulo; // Estructura que contiene los datos del rectangulo3 A, B y su area

    refRectangulo : REFERENCE TO st_Rectangulo := stRectangulo3; // Hace Referencia a stRectangulo3   
END_VAR

// Asignaci√≥n de valores a los lados de los tres rect√°ngulos.

// Asignaci√≥n de valores de los lados del rect√°ngulo 1
stRectangulo1.rAside := 44; //Valor del lado A.
stRectangulo1.rBside := 32; //Valor del lado B.

// Asignaci√≥n de valores de los lados del rect√°ngulo 2
stRectangulo2.rAside := 12.8; //Valor del lado A.
stRectangulo2.rBside := 320.4; //Valor del lado B.

// Asignaci√≥n de valores de los lados del rect√°ngulo 3
stRectangulo3.rAside := 1024.2; //Valor del lado A.
stRectangulo3.rBside := 2048.4; //Valor del lado B.

// C√°lculo del √°rea del rect√°ngulo pasando valores a la funci√≥n
stRectangulo1.rArea := Fc_AreaCalcVal(i_rAside:=stRectangulo1.rAside, i_rBside:= stRectangulo1.rBside);

// C√°lculo del √°rea del rect√°ngulo pasando un puntero a la funci√≥n
Fc_AreaCalcPoint(ADR(stRectangulo2));

// C√°lculo del √°rea del rect√°ngulo pasando una referencia a la funci√≥n
Fc_AreaCalcRef(refRectangulo);
```
En este caso puede que las diferencias pueden parecer insignificantes, puesto que la cantidad de datos que se le pasan a la funci√≥n son pocos. Pero seguidamente veremos un ejemplo con mayor n√∫mero de par√°metros de entrada para poder apreciar las ventajas del pase de par√°metros por, especialmente, referencia y tambi√©n por puntero.

### <span style="color:grey">.Caso de pase de grandes cantidades de datos a funciones:</span>
Cuando se precisa pasar estructuras con gran cantidad de datos a funciones √≥ a FB¬¥s, el pase de par√°metros por valores no es el m√©todo m√°s adecuado puesto que se requieren gran cantidad de par√°metros de entrada, cada par√°metro implica crear una nueva variable local de la funci√≥n, o del FB, lo que supone gasto de memoria y tiempo de ejecuci√≥n en copiar los datos. Caso de estructuras de datos de varios Kbytes, o arrays de centenares o miles de elementos, este m√©todo es impensable. 
En el caso de tener que pasar grandes cantidades de datos, la soluci√≥n es el empleo de punteros, o mejor a√∫n, el pase de datos por referencia. 
Seguidamente se muestra un ejemplo de una funci√≥n para calcular el valor promedio de un array de 20 elementos, pasando los valores a la funci√≥n y 
pasando los valores mediante una referencia.

### C√≥digo de la funci√≥n Fc_AverageValues para pase de valores:
```javascript
// Esta funci√≥n calcula la media de un buffer de 20 elementos. Solo a modo de ejemplo comparativo
// no ser√≠a una forma muy adecuada de hacerlo as√≠

FUNCTION Fc_AverageValues : REAL
VAR_INPUT
    i_REALV1  : REAL; //Valor posici√≥n 1
    i_REALV2  : REAL; //Valor posici√≥n 2
    i_REALV3  : REAL; //Valor posici√≥n 3
    i_REALV4  : REAL; //Valor posici√≥n 4
    i_REALV5  : REAL; //Valor posici√≥n 5
    i_REALV6  : REAL; //Valor posici√≥n 6
    i_REALV7  : REAL; //Valor posici√≥n 7
    i_REALV8  : REAL; //Valor posici√≥n 8
    i_REALV9  : REAL; //Valor posici√≥n 9
    i_REALV10 : REAL; //Valor posici√≥n 10
    i_REALV11 : REAL; //Valor posici√≥n 11
    i_REALV12 : REAL; //Valor posici√≥n 12
    i_REALV13 : REAL; //Valor posici√≥n 13
    i_REALV14 : REAL; //Valor posici√≥n 14
    i_REALV15 : REAL; //Valor posici√≥n 15
    i_REALV16 : REAL; //Valor posici√≥n 16
    i_REALV17 : REAL; //Valor posici√≥n 17
    i_REALV18 : REAL; //Valor posici√≥n 18
    i_REALV19 : REAL; //Valor posici√≥n 19
    i_REALV20 : REAL; //Valor posici√≥n 20
END_VAR

//Retorna la suma de todos los valores dividida del n√∫mero de valores que son 20.

Fc_AverageValues := (i_REALV1 + i_REALV2 + i_REALV3 + i_REALV4 + i_REALV5 + i_REALV6 + i_REALV7 + 
                    i_REALV8 + i_REALV9 + i_REALV10 + i_REALV11 + i_REALV12 + i_REALV13 + 
                    i_REALV14 + i_REALV15 + i_REALV16 + i_REALV17 + i_REALV18 + 
                    i_REALV19 + i_REALV20) / 20.0 ;

```
### C√≥digo de la funci√≥n Fc_AverageReferencia para pase por referencia:
```javascript
// Esta funci√≥n calcula la media de un buffer de 20 elementos. Solo a modo de ejemplo comparativo
// pasando valores por referencia.

FUNCTION Fc_AverageReferencia : REAL

VAR_INPUT
 i_Ref : REFERENCE TO ARRAY[1..20] OF REAL;
END_VAR
VAR
 intIdx : INT;      // Variable indice para el bucle.
 rVAcum : REAL:=0; // Valor acumulado.
END_VAR

// Retorna la suma de todos los valores divida del n√∫mero de valores que son 20.

FOR intIdx:=1 TO 20 BY 1 DO
 rVAcum := rVAcum + i_Ref[intIdx];
END_FOR;
Fc_AverageReferencia := rVAcum / 20.0;
```
### C√≥digo de ejemplo de llamada a ambas funciones:
```javascript
PROGRAM SR_Main_04
VAR
 arFIFO  : ARRAY[1..20] OF REAL; // FIFO con los valores de fuerza registrados.
 intIdx  : INT;                  // Variable de indice.
 rIncAng : REAL;                 // Valor de incremento angular para generaci√≥n de senoide.
 rValAng : REAL;                 // Valor actual de angulo.
 rValSin : REAL;                 // Amplitud de la senoide superpuesta.
 rVMed   : REAL;                 // Resultado del c√°lculo.

 refFIFO : REFERENCE TO ARRAY[1..20] OF REAL := arFIFO; // Crea una referencia y la asigna a arFIFO
 rMedRef : REAL;           // Resultado del c√°lculo para el ejemplo de pase de valores por Ref.
END_VAR

// Ejemplo de como realizar el c√°lculo del valor medio de las lecturas de fuerza contenidas en arFIFO
// mediante la funci√≥n Fc_AverageValues (Pase de par√°metros por valores) y Fc_AverageReferencia (Pase de
// par√°metros por referencia)
// Lo que se pretende es ver las ventajas del pase por referencia

// Asignaci√≥n de valores para llenar el FIFO a efectos de tener algunos valores para el c√°lculo de la media
// al valor 124 le superpone una variaci√≥n senoidal de amplitud 6

rIncAng := (2 * 3.14159) / 20.0; // 2 * PI Radianes dividido entre 20 puntos.
rValAng := 0.0;                   // Valor inicial del angulo.

FOR intIdx :=1 TO 20 BY 1 DO
 rValSin := SIN(rValAng) * 6;         // Valor del seno para una amplitud de 6
 arFIFO[intIdx] := 124.0 + rValSin;  // A un nivel de 124.0 se superpone un seno de amplitud 6.
 rValAng := rValAng + rIncAng;       // Pr√≥ximo valor angular. 
END_FOR;

// Con el FIFO de valores llamaremos a la funci√≥n para el c√°lculo de la media pasando valores.
// Lo que no ser√≠a para nada adecuado por tratarse de muchos par√°metros.

rVMed:= Fc_AverageValues( i_REALV1  := arFIFO[1],
                          i_REALV2  := arFIFO[2],
                          i_REALV3  := arFIFO[3],
                          i_REALV4  := arFIFO[4], 
                          i_REALV5  := arFIFO[5],
                          i_REALV6  := arFIFO[6], 
                          i_REALV7  := arFIFO[7],
                          i_REALV8  := arFIFO[8],
                          i_REALV9  := arFIFO[9],
                          i_REALV10 := arFIFO[10],
                          i_REALV11 := arFIFO[11],
                          i_REALV12 := arFIFO[12],
                          i_REALV13 := arFIFO[13],
                          i_REALV14 := arFIFO[14],
                          i_REALV15 := arFIFO[15],
                          i_REALV16 := arFIFO[16],
                          i_REALV17 := arFIFO[17],
                          i_REALV18 := arFIFO[18],
                          i_REALV19 := arFIFO[19],
                          i_REALV20 := arFIFO[20]);

// Con el FIFO lleno de valores llamaremos a la funci√≥n para el c√°lculo de la media pasando valores por referencia
// para ver lo sencillo que resulta en este caso.

rMedRef := Fc_AverageReferencia(i_Ref:=refFIFO);
```
Claramente la llamada a la funci√≥n pasando los valores por referencia es la mejor.
Y en este ejemplo se ha supuesto un ejemplo con solo 20 datos de entrada,
pero lo normal es encontrar aplicaciones con estructuras de datos de varios Kbytes.
***
- Un puntero de tipo T apunta a un objeto de tipo T (T = tipo de datos b√°sico o definido por el usuario)
- Un puntero contiene la direcci√≥n del objeto al que apunta.
- La operaci√≥n fundamental con un puntero se llama "desreferenciar". La desreferenciaci√≥n en CODESYS se realiza con el s√≠mbolo "^"
- Un puntero puede apuntar a un objeto diferente en un momento diferente.
- Antes de desreferenciar un puntero y asignarle un valor, siempre debe verificar si un puntero apunta a un objeto. (puntero = 0)?
- Una referencia del tipo T "apunta" a un objeto del tipo T (T = tipo de datos b√°sico o definido por el usuario).
- Una referencia debe ser inicializada con un objeto y su "apuntando" a este objeto a trav√©s del programa.
- Una referencia no debe ser desreferenciada como un puntero y puede usarse con la misma sintaxis que el objeto.
- Otra palabra de referencia es "Alias" (otro nombre) un seud√≥nimo para el objeto.
- La referencia no tiene direcci√≥n propia y un puntero s√≠. La direcci√≥n de la referencia es la misma que la del objeto "puntiagudo".
- No hay referencia 0, por lo que nunca debe llamar a la referencia si no est√° inicializada.
- Debe verificar si tiene una referencia v√°lida con la palabra clave integrada CODESYS "__ISVALIDREF".

El mejor uso de punteros y referencias es cuando desea pasar o devolver un objeto de alg√∫n tipo a una funci√≥n o bloque de funciones por "referencia" porque el objeto es demasiado grande o desea manipular el objeto pasado dentro de la funci√≥n/bloque de funci√≥n. Aseg√∫rese de que el lector de su c√≥digo sepa que va a cambiar el valor del objeto dentro de la funci√≥n/bloque de funciones si esto es lo que pretende hacer cuando lo pasa como argumento.

### <span style="color:grey">.Resumen / Conclusiones:</span>

- **La memoria contiene** miles y hasta millones de celdas o byte, en las que se ubica el c√≥digo del programa y todos los datos/variables. Cada celda tiene su n√∫mero, al que se llama direcci√≥n de memoria y que se suele expresar en hexadecimal 16#FA1204 -como ejemplo-
- **Un puntero es una variable**, que en lugar de contener un valor contiene una direcci√≥n de memoria, en la que ‚Äúvive‚Äù la variable a la que realmente queremos acceder. 
- Al igual que cualquier otra variable, **hay que declarar los punteros** para que el compilador pueda ubicarlos en la memoria. 
Recordemos que un puntero es una variable, pero que su contenido es una direcci√≥n de memoria.
- **Para cada tipo de variable** se precisa el correspondiente **tipo de puntero**. 
No se puede acceder a una variable INT con un puntero pensado para acceder a una estructura de datos.
- Nada tiene que ver el **acceso indirecto** a un array mediante una variable de √≠ndice, con un puntero. 
En este caso el acceso est√° limitado al propio array, con el puntero se puede acceder a cualquier direcci√≥n de memoria.
- **Con punteros se puede acceder a todo tipo de datos**, en una simple l√≠nea de c√≥digo se puede copiar una estructura entera de varios Kbytes de datos. Lo que resulta mucho m√°s r√°pido.
- Una referencia se parece mucho a un puntero, para simplificar podr√≠amos decir que es un ‚Äúalias‚Äù de un objeto y que es algo menos cr√≠tico que los punteros, su principal utilidad es la de pasar gran cantidad de par√°metros a funciones, de forma muy simple y r√°pida.
- **El pase de par√°metros a una funci√≥n** se puede realizar de diversas formas, por valores, por punteros o por referencia, el programador deber√° elegir el m√°s adecuado para cada aplicaci√≥n.
- Cuando se trata de **grandes cantidades de datos** el pase de par√°metros por referencia o por punteros, ser√°n los adecuados

***
### <span style="color:grey">Links de Puntero y Referencia:</span>
- üîó [Perre Garriga,Pointer&Reference](https://www.infoplc.net/blogs-automatizacion/item/112457-punteros-programacion-plcs-basico-que-hay-que-saber)

- üîó [Control and use of Pointers In Codesys](https://www.youtube.com/watch?v=I_wdSAyJ6LE)

- üîó [help.codesys.com, Pointers](https://help.codesys.com/api-content/2/codesys/3.5.14.0/en/_cds_datatype_pointer/#ca488150a6a89ac0a8640e013d12b7-id-2afac6cf50a6a899c0a8640e001ca61a)

- üîó [AT&U, CODESYS - Difference between pointer and reference](https://www.youtube.com/watch?v=CAeWLPaUeuM)

- üîó [AT&U, CODESYS -Differente between pass by vale and pass by Reference](https://www.youtube.com/watch?v=rUfDD5FTgE0)

- üîó [Ninja Monkeys Tutorials, 21. TwinCAT 3: Pointer and References](https://www.youtube.com/watch?v=av0Lpw5KdjE)

- üîó [Tutorial #26: Zeiger (Pointer) in CoDeSys](https://www.youtube.com/watch?v=xccZTZLvinQ)

- üîó [www.hemelix.com, structured-text-memory-management](https://www.hemelix.com/plc/structured-text-memory-management/)
***
### <span style="color:grey">Link al Video de Youtube 011:</span>
- üîó [011 - OOP IEC 61131-3 PLC -- Puntero vs Referencia](https://youtu.be/AnJZmwQWaYQ)